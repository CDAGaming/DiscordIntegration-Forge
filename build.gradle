buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}
plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
repositories {
    jcenter()
    maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }// Maybe needed for other mods in the future
    maven { url "https://maven.latmod.com/" } //FTB
    maven { url 'https://jitpack.io' }
}
version = "1.1.19"
group = "de.erdbeerbaerlp.dcintegration" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "dcintegration"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}
 
minecraft {

    mappings channel: 'snapshot', version: '20171003-1.12'
    runs {
        server {

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }
    }
}
configurations {
    embed
    compile.extendsFrom(embed)
}
reobf {
    shadowJar {
        dependsOn tasks.createMcpToSrg
        mappings = tasks.createMcpToSrg.outputs.files.singleFile
    }
}
shadowJar {
    classifier = '1.12'
    configurations = [project.configurations.embed]
    relocate 'okhttp3', 'dcshadow.okhttp'
    relocate 'okio', 'dcshadow.okio'
    relocate 'club.minnced', 'dcshadow.club.minnced'
    relocate 'com.sun', 'dcshadow.com.sun'
    relocate 'com.iwebpp', 'dcshadow.com.iwebpp'
    relocate 'com.neovisionaries', 'dcshadow.com.neovisionaries'
    relocate 'gnu', 'dcshadow.gnu'
    relocate 'org.apache.commons.collections4', 'dcshadow.apache-collections4'
    relocate 'org.slf4j', 'dcshadow.slf4j'
    relocate 'org.json', 'dcshadow.org.json'
    relocate 'org.intellij', 'dcshadow.intellij'
    relocate 'org.jetbrains', 'dcshadow.jetbrains'
    relocate 'tomp2p', 'dcshadow.tomp2p'
    relocate 'com.fasterxml', 'dcshadow.fasterxml'
}
dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'
	embed("org.spongepowered:mixin:0.8-SNAPSHOT") {
        transitive = false
    }
    embed("net.dv8tion:JDA:4.2.0_211")
    embed 'club.minnced:discord-webhooks:0.1.8'
    compile "com.feed_the_beast.mods:FTBUtilities:5.+"
    compile "com.feed_the_beast.mods:FTBLib:5.+"
    compile 'com.github.upcraftlp:votifier:master-SNAPSHOT'
}
artifacts {
    archives tasks.shadowJar
}
mixin {
    add sourceSets.main, 'mixins.dcintegration.refmap.json'
}

jar {
    classifier = "1.12"
    manifest {
        attributes([
                'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp': System.currentTimeMillis(),
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
        		'MixinConfigs': 'mixins.dcintegration.json',
        		"ForceLoadAsMod": "true",
        		'FMLCorePluginContainsFMLMod': 'true'
        ])
    }
}
curseforge {
    // $GRADLE_USER_HOME/gradle.properties
    if (project.hasProperty('curseforge.apikey')) {
        apiKey = getProperty("curseforge.apikey")
        project {
            id = '324952'
            changelog = project.changelog
            releaseType = 'release'
            addGameVersion '1.12.2'
            mainArtifact(jar) {
                displayName = "DiscordIntegration-$version (MC 1.12)"
            }
            relations {
                optionalDependency 'ftb-utilities'
                optionalDependency 'emojicord'
                optionalDependency 'voting'
            }
        }
    }
}
tasks.curseforge.dependsOn(build)